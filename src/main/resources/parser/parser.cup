/* ----------------------Preliminary Declarations Section--------------------*/

/* Import the class java_cup.runtime.*  */
import java_cup.runtime.ComplexSymbolFactory;

import com.werner.compiler.ast.Program;
import com.werner.compiler.ast.statements.AssignStatement;
import com.werner.compiler.ast.statements.EmptyStatement;
import com.werner.compiler.ast.statements.Statement;
import com.werner.compiler.ast.expressions.Expression;
import com.werner.compiler.ast.expressions.BinaryExpression;
import com.werner.compiler.ast.Identifier;
import com.werner.compiler.ast.expressions.IntLiteral;
import com.werner.compiler.ast.Type;
import com.werner.compiler.ast.statements.IfStatement;
import com.werner.compiler.ast.expressions.BooleanLiteral;
import com.werner.compiler.ast.expressions.Operator;
import com.werner.compiler.ast.expressions.VariableExpression;

import java.util.List;
import java.util.ArrayList;
import java.util.Optional;

/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:

    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {

        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");

        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }

        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);

        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }

    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

terminal                                        SEMICOLON, PLUS, MINUS, STAR, SLASH, LPAREN, RPAREN, ASGN, LCURLYBRACE, RCURLYBRACE, IF, ELSE;
terminal            Integer                     NUMBER;
terminal            String                      IDENT;
terminal                                        TRUE, FALSE;

nonterminal         Expression                  expr, term, factor;
nonterminal         Object                      program;
nonterminal         List<Statement>             statement_list;
nonterminal         Statement                   statement;
nonterminal         AssignStatement             assign_statement;
nonterminal         EmptyStatement              empty_statement;
nonterminal         IfStatement                 if_statement;

/* ----------------------------Grammar Section-------------------- */

    program             ::= LCURLYBRACE statement_list:stmnts RCURLYBRACE
                        {: RESULT = new Program(new ComplexSymbolFactory.Location(stmntsleft, stmntsright), stmnts); :}
                        ;

    statement_list      ::= /* epsilon */
                        {: RESULT = new ArrayList(); :}

                        |   statement_list:statement_list statement:statement
                        {: statement_list.add(0, statement); RESULT = statement_list; :}
                        ;

    statement           ::= assign_statement:assign_statement
                        {: RESULT = assign_statement; :}
                        |   empty_statement:empty_statement
                        {: RESULT = empty_statement; :}
                        |   if_statement:if_statement
                        {: RESULT = if_statement; :}
                        ;

    empty_statement     ::= SEMICOLON:semicolon
                        {: RESULT = new EmptyStatement(new ComplexSymbolFactory.Location(semicolonleft, semicolonright)); :}
                        ;

    assign_statement    ::= IDENT:identifier ASGN:asgn expr:expression SEMICOLON
                        {: RESULT = new AssignStatement(new ComplexSymbolFactory.Location(asgnleft, asgnright), new Identifier(new ComplexSymbolFactory.Location(identifierleft, identifierright), identifier), expression); :}
                        ;

    if_statement        ::= IF:if_token LPAREN:lparen expr:expression RPAREN:rparen LCURLYBRACE:if_lcurlybrace statement_list:statement_list RCURLYBRACE:if_rcurlybrace
                        {: RESULT = new IfStatement(new ComplexSymbolFactory.Location(if_tokenleft, if_rcurlybraceright), expression, statement_list, Optional.empty()); :}
                        |   IF:if_token LPAREN:lparen expr:expression RPAREN:rparen LCURLYBRACE:if_lcurlybrace statement_list:if_statement_list RCURLYBRACE:if_rcurlybrace ELSE:else_token LCURLYBRACE:else_lcurlybrace statement_list:else_statement_list RCURLYBRACE:else_rcurlybrace
                        {: RESULT = new IfStatement(new ComplexSymbolFactory.Location(if_tokenleft, if_rcurlybraceright), expression, if_statement_list, Optional.of(else_statement_list)); :}
                        ;

    expr                ::= expr:expression PLUS:operator term:term
                        {: RESULT = new BinaryExpression(new ComplexSymbolFactory.Location(operatorleft, operatorright), Type.INTEGER, Operator.ADD, expression, term); :}
                        |   expr:expression MINUS:operator term:term
                        {: RESULT = new BinaryExpression(new ComplexSymbolFactory.Location(operatorleft, operatorright), Type.INTEGER, Operator.SUB, expression, term); :}
                        |   term:term
                        {: RESULT = term; :}
                        ;

    term                ::= term:term STAR:operator factor:factor
                        {: RESULT = new BinaryExpression(new ComplexSymbolFactory.Location(operatorleft, operatorright), Type.INTEGER, Operator.MUL, term, factor); :}
                        |   term:term SLASH:operator factor:factor
                        {: RESULT = new BinaryExpression(new ComplexSymbolFactory.Location(operatorleft, operatorright), Type.INTEGER, Operator.DIV, term, factor); :}
                        |   factor:factor
                        {: RESULT = factor; :}
                        ;

    factor              ::= NUMBER:number
                        {: RESULT = new IntLiteral(new ComplexSymbolFactory.Location(numberleft, numberright), number); :}
                        |   LPAREN:lparen expr:expression RPAREN:rparen
                        {: RESULT = expression; :}
                        |   MINUS:operator factor:factor
                        {: RESULT = new BinaryExpression(new ComplexSymbolFactory.Location(operatorleft, operatorright), Type.INTEGER, Operator.SUB, new IntLiteral(new ComplexSymbolFactory.Location(operatorleft, operatorright), 0), factor); :}
                        |   IDENT:identifier
                        {: RESULT = new VariableExpression(new ComplexSymbolFactory.Location(identifierleft, identifierright), new Identifier(new ComplexSymbolFactory.Location(identifierleft, identifierright), identifier), null); :}
                        |   TRUE:true_token
                        {: RESULT = new BooleanLiteral(new ComplexSymbolFactory.Location(true_tokenleft, true_tokenright), true); :}
                        |   FALSE:false_token
                        {: RESULT = new BooleanLiteral(new ComplexSymbolFactory.Location(false_tokenleft, false_tokenright), false); :}
                        ;