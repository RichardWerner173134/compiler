/* ----------------------Preliminary Declarations Section--------------------*/

/* Import the class java_cup.runtime.*  */
import java_cup.runtime.*;

/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:

    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {

        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");

        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }

        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);

        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }

    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};

/* ------------Declaration of Terminals and Non Terminals Section----------- */

terminal                        SEMICOLON, PLUS, MINUS, TIMES, DIVIDE, LPAREN, RPAREN;
terminal            Integer     NUMBER, IDENT;

nonterminal         Integer     expr, term, factor;
nonterminal         Object      expr_part;

/* ----------------------------Grammar Section-------------------- */

    expr_part ::= expr:e
                 {: System.out.println(" = " + e); :}
                 SEMICOLON
                 ;

    expr ::= expr:e PLUS term:t
            {: RESULT = new Integer(e.intValue() + t.intValue()); :}
            | expr:e MINUS term:t
            {: RESULT = new Integer(e.intValue() - t.intValue()); :}
            | term:t
            {: RESULT = new Integer(t.intValue()); :}
            ;

    term ::= term:t TIMES factor:f
            {: RESULT = new Integer(t.intValue() * f.intValue()); :}
            | term:t DIVIDE factor:f
            {: RESULT = new Integer(t.intValue() / f.intValue()); :}
            | factor:f
            {: RESULT = new Integer(f.intValue()); :}
            ;

    factor ::= NUMBER:n
            {: RESULT = new Integer(n.intValue()); :}
            | LPAREN expr:e RPAREN
            {: RESULT = new Integer(e.intValue()); :}
            | MINUS factor:f
            {: RESULT = new Integer(f.intValue()); :}
            ;
